buildscript {
    ext {
        sdk_version = 34
        min_api_level = 26
        google_services_version = '4.4.0'
        crashlytics_version = '2.9.9'
    }
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$crashlytics_version"
    }
}

plugins {
    alias libs.plugins.detekt
    alias libs.plugins.android.application apply false
    alias libs.plugins.kotlin apply false
    alias libs.plugins.ksp
    alias libs.plugins.hilt apply false
    alias(libs.plugins.compose.compiler) apply false
}

detekt {
    source.setFrom(rootProject.rootDir)
    config.setFrom("$projectDir/detekt.yml")
    buildUponDefaultConfig = true
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}


subprojects {
    def currentModule = it.name

    if (currentModule == 'app') {
        def isReleaseBuildType = gradle.startParameter.taskRequests.toString().contains("Release")
        if (isReleaseBuildType) {
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register("ci") {
    it.dependsOn("app:test", "app:lint", "app:assembleDebug", "detekt")
}