buildscript {
    ext {
        sdk_version = 33
        min_api_level = 26
        gradle_version = '7.3.0'
        kotlin_version = '1.7.20'
        coroutines_version = '1.6.4'
        junit_version = '4.13.2'
        hilt_version = '2.44'
        room_version = '2.4.3'
        android_compat = '1.5.1'
        compose_bom_version = '2023.01.00'
        compose_compiler_version = '1.3.2'
        moshi_version = '1.13.0'
        google_services_version = '4.3.14'
        crashlytics_version = '2.9.2'
        firebase_bom_version = '31.0.1'
        androidX_test_version = '1.5.0'
    }
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$crashlytics_version"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.18.1")
}

detekt {
    source = files(rootProject.rootDir)
    config = files("$projectDir/detekt.yml")
    buildUponDefaultConfig = true

    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination = file("$buildDir/reports/detekt/detekt-checkstyle.xml")
        html.destination = file("$buildDir/reports/detekt/detekt-report.html")
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    def currentModule = it.name

    if (currentModule == 'app') {
        def isReleaseBuildType = gradle.startParameter.taskRequests.toString().contains("Release")
        if (isReleaseBuildType) {
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
