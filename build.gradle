buildscript {
    ext {
        sdk_version = 34
        min_api_level = 26
        gradle_version = '8.2.0'
        kotlin_version = '1.9.21'
        hilt_version = '2.49'
        compose_compiler_version = '1.5.6'
        google_services_version = '4.4.0'
        crashlytics_version = '2.9.9'
    }
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$crashlytics_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.23.4"
    alias libs.plugins.android.application apply false
    alias libs.plugins.kotlin apply false
    alias libs.plugins.ksp
    alias libs.plugins.hilt apply false
}

detekt {
    source.setFrom(rootProject.rootDir)
    config.setFrom("$projectDir/detekt.yml")
    buildUponDefaultConfig = true
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}


subprojects {
    def currentModule = it.name

    if (currentModule == 'app') {
        def isReleaseBuildType = gradle.startParameter.taskRequests.toString().contains("Release")
        if (isReleaseBuildType) {
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register("ci") {
    it.dependsOn("app:test", "app:lint", "app:assembleDebug", "detekt")
}