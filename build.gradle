buildscript {
    ext {
        sdk_version = 31
        min_api_level = 21
        gradle_version = '7.0.0'
        kotlin_version = '1.6.10'
        coroutines_version = '1.5.1'
        junit_version = '4.13.2'
        hilt_version = '2.38.1'
        activity_ktx_version = '1.2.3'
        room_version = '2.3.0'
        android_compat = '1.4.2'
        gson = '2.8.9'
    }
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        apply from: "dependencies.gradle"

        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}

// Defining common gradle plugins, config and dependencies to every module.
// There are some differences between app module and the others,
subprojects {
    def currentModule = it.name
    def appModule = 'app'

    if (currentModule == appModule) {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dagger.hilt.android.plugin'

    android {
        compileSdkVersion sdk_version

        if (currentModule != appModule) {
            defaultConfig {
                minSdkVersion min_api_level
                targetSdkVersion sdk_version
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        lintOptions {
            warningsAsErrors true
            checkDependencies true
            lintConfig file("lint.xml")
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "com.google.dagger:hilt-android:$hilt_version"
        kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
